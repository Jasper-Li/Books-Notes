###1. Exercise
Calculate the summary of even-numbers from 1 to 10000, and print the result.   
Respectively, using do-while, while and for statement.

###2. Smali Code of do-while statement
	
	.method public static main([Ljava/lang/String;)V
		.registers 6

		.prologue
		.line 11
		const-wide/16 v1, 0x0

		.line 12
		const/4 v0, 0x0

		.line 14
		:cond_3
		add-int/lit8 v0, v0, 0x2

		.line 15
		int-to-long v3, v0

		add-long/2addr v1, v3

		.line 16
		const/16 v3, 0x2710

		if-lt v0, v3, :cond_3

		.line 18
		##print the value of v1

	.end method
	
####(1) Key opcodes Description
**const-wide/16 vx, lit16**  
Push the integer constant into vx and vx+1 registers, expanding the integer constant into a long constant.  

**const/4 vx, lit4**  
Push the 4bits constant into vx.

**add-int/lit8 vx, vy, lit8**  
vx = vy + lit8  

**int-to-long vx, vy**  
Convert the integer in vy into a long in vx,vx+1.

**add-long/2addr vx, vy**  
Adds the long value in vy,vy+1 to the long value in vx,vx+1.

**const/16 vx, lit16**
Push the 16 bit constant into vx.

**if-lt vx, vy, target**  
Jump to target if vx < vy, and vx vy are integers.

####2.2 Concise smali code

	const-wide/16 v1, 0x0	##1.initial values
	const/4 v0, 0x0
	
	:cond_3					##2. loop start
	
	add-int/lit8 v0, v0, 0x2	##3. loop action
	
	int-to-long v3, v0
	add-long/2addr v1, v3
	
	const/16 v3, 0x2710		##4.loop condition judgment
	if-lt v0, v3, :cond_3
							##5. loop end
	
####2.3 Java Code
		long sum=0;
		int num=0;

		do{ num += 2;
			sum += num;
		}while (num < 10000);

###3. Smali code of while statement

	.method public static main([Ljava/lang/String;)V
		.registers 6

		.prologue
		.line 11
		const-wide/16 v1, 0x0

		.line 12
		const/4 v0, 0x0

		.line 14
		:goto_3
		const/16 v3, 0x2710

		if-gt v0, v3, :cond_c

		.line 16
		int-to-long v3, v0

		add-long/2addr v1, v3

		.line 17
		add-int/lit8 v0, v0, 0x2

		goto :goto_3

		.line 20
		:cond_c
		## Print v1 value 
	.end method

####3.1 Key Opcodes
**if-gt vx, vy, targe**  
Jump to target if vx > vy, and vx vy are integers.

####3.2 Concise smali code
	
	const-wide/16 v1, 0x0	##1. initial value
	const/4 v0, 0x0

	:goto_3					##2. loop start
	
	const/16 v3, 0x2710		##3. condition judgment
	if-gt v0, v3, :cond_c
	
	int-to-long v3, v0		##4. loop action
	add-long/2addr v1, v3

	add-int/lit8 v0, v0, 0x2

	goto :goto_3			##5. loop back

	:cond_c					##6. out of loop
	##print v1 value

####3.3 Java Code
		long sum=0;
		int num=0;
		
		while(num <= 10000)
		{
			sum += num;
			num += 2;
		}

###4. Smali Code of for statement
The smali code of while statement is the **same** as the while statement.

	.method public static main([Ljava/lang/String;)V
		.registers 6

		.prologue
		.line 11
		const-wide/16 v1, 0x0

		.line 12
		const/4 v0, 0x0

		:goto_3
		const/16 v3, 0x2710

		if-gt v0, v3, :cond_c

		.line 13
		int-to-long v3, v0

		add-long/2addr v1, v3

		.line 12
		add-int/lit8 v0, v0, 0x2

		goto :goto_3

		.line 15
		:cond_c
		##print v1 value

	.end method

####4.1 Java Code

	long sum = 0;
	for(int num=0; num <= 10000; num +=2)
		sum += num;

	
